#!/usr/bin/env perl

use strict;
use warnings;
use 5.010;

use File::Slurp qw(write_file);
use List::Util qw(sum);
use LWP::UserAgent;

our $VERSION = '0.01';

my $ua = LWP::UserAgent->new;
$ua->timeout(5);

sub spew {
	my ( $file, $value ) = @_;

	return write_file( $file, { err_mode => 'quiet' }, "${value}\n" );
}

sub get_sensor {
	my ( $bus, $id, $ip, $name ) = @_;

	spew( "/tmp/${bus}/${id}", 1 );
	system( 'dorfmap_set_remote', $bus );

	sleep(7);

	my @temps;
	my @brightness;

	for my $i ( 0 .. 12 ) {
		my $res = $ua->get("http://${ip}/?uled=on");

		if ( $res->is_success ) {
			my $data = $res->content;
			my ( $temp, $lsb )
			  = ( $data =~ m{ temph = (\d+) \n templ = (\d+) }x );
			my ($brightness) = ( $data =~ m{ brightness = (\d+) \n }x );
			if ( $lsb > 127 ) {
				$temp += 0.5;
			}
			push( @temps,      $temp );
			push( @brightness, $brightness );
		}

		sleep(1);
	}

	if (@temps) {
		my $temp       = sum(@temps) / @temps;
		my $brightness = sum(@brightness) / @brightness;

		spew( "/srv/www/bgdata/${name}_temp",       $temp );
		spew( "/srv/www/bgdata/${name}_brightness", $brightness );
	}

	spew( "/tmp/${bus}/${id}", 0 );
	system( 'dorfmap_set_remote', $bus );
}

get_sensor( 'feedback3', '9',    '172.22.26.14', 'lounge_screen' );
get_sensor( 'feedback9', 'bin7', '172.22.26.15', 'hackcenter' );
