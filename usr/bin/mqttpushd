#!/usr/bin/env perl

use strict;
use warnings;
use 5.010;

use File::Slurp qw(read_file write_file);
use Net::MQTT::Simple;

my $flukso_prefix = '/srv/www/flukso';
my $mqtt          = Net::MQTT::Simple->new('mqttserver');

sub slurp {
	my ($file) = @_;

	my $content = read_file( $file, err_mode => 'quiet' );
	if ( defined $content ) {
		chomp $content;
	}
	return $content;
}

my $loop_cnt = 0;

while (1) {

	$loop_cnt = ( $loop_cnt + 1 ) % 8;

	for my $phase ( 1 .. 3 ) {
		my $reading = slurp("${flukso_prefix}/now_p${phase}");
		if ( defined $reading and $reading >= 0 ) {
			$mqtt->publish( "sensors/flukso/power/L${phase}/now" => $reading );
		}
	}

	if ( $loop_cnt == 0 ) {
		for my $phase ( 1 .. 3 ) {
			my $reading = slurp("${flukso_prefix}/30_p${phase}");
			if ( defined $reading and $reading >= 0 ) {
				$mqtt->publish( "sensors/flukso/power/L${phase}/30s_average" =>
					  sprintf( '%.1f', $reading ) );
			}
		}
		my $reading = slurp("${flukso_prefix}/30");
		if ( defined $reading and $reading >= 0 ) {
			$mqtt->publish( "sensors/flukso/power/sum/30s_average" =>
				  sprintf( '%.1f', $reading ) );
		}
	}

	sleep(1);
}
