#!/usr/bin/env perl

use strict;
use warnings;
use 5.010;

use DateTime;
use File::Slurp qw(read_file write_file);
use LWP::UserAgent;

our $VERSION = '0.01';

my $ua = LWP::UserAgent->new;
$ua->timeout(2);

my $re_autooff = qr{
	^
	(?: \S+ ) \s+ # device
	(?: \S+ ) \s+ # x
	(?: \S+ ) \s+ # y
	(?: \S+ ) \s+ # w
	(?: \S+ ) \s+ # h
	(?<path> \S+)
	.* autooff = (?<duration> \d+) (?<unit> [dhms])
}x;

my %units = (
	s => 1,
	m => 60,
	h => 60 * 60,
	d => 60 * 60 * 24,
);

sub slurp {
	my ($file) = @_;

	my $content = read_file( $file, err_mode => 'quiet' );
	if ( defined $content ) {
		chomp $content;
	}
	return $content;
}

sub spew {
	my ( $file, $value ) = @_;

	return write_file( $file, { err_mode => 'quiet' }, $value );
}

while (1) {

	my %autooff;

	my $raw_coordinates = read_file('/etc/dorfmap/coordinates');
	$raw_coordinates =~ s{\\\n}{}gs;

	for my $line ( split( /\n/, $raw_coordinates ) ) {
		if ( $line =~ $re_autooff ) {
			my $device = '/tmp/' . $+{path};
			my $delay  = $+{duration} * $units{ $+{unit} };

			if ( -e $device and slurp($device) == 1 ) {
				my $now = DateTime->now( time_zone => 'Europe/Berlin' )->epoch;
				my $modified = ( stat($device) )[9];
				if ( ( $now - $modified ) > $delay ) {
					spew( $device, "0\n" );
					system( '/usr/lib/dorfmap/dorfbus-write',
						( split( /\//, $device ) )[2] );
				}
			}
		}
	}

	my $res = $ua->get('http://door/status');
	if ( $res->is_success ) {
		write_file( '/srv/www/doorstatus', $res->decoded_content );
	}
	else {
		write_file( '/srv/www/doorstatus', -1 );
	}

	sleep(120);
}

__END__

=head1 NAME

=head1 SYNOPSIS

=head1 VERSION

=head1 DESCRIPTION

=head1 OPTIONS

=over

=back

=head1 EXIT STATUS

=head1 CONFIGURATION

None.

=head1 DEPENDENCIES

=over

=back

=head1 BUGS AND LIMITATIONS

=head1 AUTHOR

Copyright (C) 2013 by Daniel Friesel E<lt>derf@finalrewind.orgE<gt>

=head1 LICENSE

  0. You just DO WHAT THE FUCK YOU WANT TO.
